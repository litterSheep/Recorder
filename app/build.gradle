apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

greendao {
    schemaVersion 16
    daoPackage 'com.ly.recorder.db.greendao'
    targetGenDir 'src/main/java'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.ly.recorder"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [APP_CHANNEL_VALUE: "wandoujia"]
    }
    buildTypes {
        release {
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            //移除无用Resources文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                File outputDirectory = new File(outputFile.parent);
                def fileName
                if (variant.buildType.name == "release") {
                    fileName = "Recorder_${variant.productFlavors[0].name}_v${defaultConfig.versionName}.apk"
                } else {
                    fileName = "app_v${defaultConfig.versionName}_degug.apk"
                }
                output.outputFile = new File(outputDirectory, fileName)
            }
        }
    }
    productFlavors {
        wandoujia {
            applicationId 'com.ly.recorder'
        }
        //批量配置
        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [APP_CHANNEL_VALUE: name]
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'org.greenrobot:greendao-generator:3.2.0'
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.github.flavienlaurent.datetimepicker:library:0.0.2'
}
